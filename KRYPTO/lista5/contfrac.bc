/* Computes continued fraction expansions of some nice numbers.

   Usage: bc -l contfrac.bc
*/

define floor(x) {
   auto s
   s = scale
   scale = 0
   x /= 1
   scale = s
   return x
}

define abs(x) {
   if (x < 0)
      return -x
   return x
}

define void expand(x) {
   auto x0, eps, a, p, q, p1, q1, p2, q2, n

   scalex = scale - 5  /* ensure the are at least 5 meaningful digits of x */

   print "Decimal approximation to ", scale, " digits:\n"
   print x, "...\n"
   print "Continued fraction approximation to precision 10^-", scalex, ":\n"
   print "["

   x0 = x
   eps = 10 ^ -scalex
   a = floor(x)
   p = 1; q = 0; p1 = 0; q1 = 1
   n = 0
   while (1) {
      n += 1
      print a, ", "
      if (x == a)
         break
      p2 = p; q2 = q
      p = a * p + p1; q = a * q + q1;
      p1 = p2; q1 = q2
      if (abs(p/q - x0) < eps)
         break
      x = 1 / (x-a)
      a = floor(x)
   }

   print "...]; ", n, " elements\n"
   print "p/q = ", p, " / ", q, "\n"
}

scale = 1000

print "\nAPPROXIMATIONS OF π\n"
print "Apparently there is no pattern in the continued fraction expansion.\n"
expand(4 * a(1))

print "\nAPPROXIMATIONS OF e\n"
print "Continued fraction is not periodic in the strict sense,\n"
print "but there is an obvious pattern!\n"
expand(e(1))

print "\nAPPROXIMATIONS OF (e+1)/(e-1)\n"
print "Another nice pattern! Converges very quickly.\n"
expand((e(1)+1)/(e(1)-1))

scale = 200

print "\nAPPROXIMATIONS OF J_1(2)/J_0(2)\n"
print "For even nicer expansion we would need the modified Bessel functions.\n"
expand(j(1,2)/j(0,2))

print "\nAPPROXIMATIONS OF √(13)\n"
print "Continued fraction is periodic, as expected from a quadratic irrational.\n"
expand(sqrt(13))

print "\nAPPROXIMATIONS OF THE GOLDEN RATIO φ = (1+√5)/2\n"
print "The most difficult number to approximate by rationals!\n"
expand((1+sqrt(5))/2)

print "\nAPPROXIMATIONS OF THE SILVER RATIO δ = 1 + √2\n"
print "Compare to the golden ratio.\n"
expand(1 + sqrt(2))

print "\nAPPROXIMATIONS OF THE BRONZE RATIO (3 + √(13))/2\n"
print "You see the pattern.\n"
expand((3 + sqrt(13))/2)

quit
